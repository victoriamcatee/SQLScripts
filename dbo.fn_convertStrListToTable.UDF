SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER ON
GO


/****** Object:  User Defined Function dbo.fn_convertStrListToTable    Script Date: 11/4/2003 5:06:42 PM ******/

CREATE FUNCTION [dbo].[fn_convertStrListToTable]
(	
	@delimiter varchar(1),
	@list ntext
)
RETURNS @tbl TABLE
(
	listpos int IDENTITY(1, 1) NOT NULL,
	string nvarchar(4000) NOT NULL
)
AS
BEGIN
	declare
		@pos int,
		@textpos int,
		@chunklen smallint,
		@str nvarchar(4000),
		@tmpstr nvarchar(4000),
		@leftover nvarchar(4000)

	set @textpos = 1
	set @leftover = ''
	while @textpos <= datalength(@list) / 2
	begin
		set @chunklen = 4000 - datalength(@leftover) / 2
		set @tmpstr = ltrim(@leftover + substring(@list, @textpos, @chunklen))
		set @textpos = @textpos + @chunklen

		set @pos = charindex(@delimiter, @tmpstr)
		while @pos > 0
		begin

			set @str = substring(@tmpstr, 1, @pos - 1)
			insert @tbl (string) values(@str)
			set @tmpstr = ltrim(substring(@tmpstr, @pos + 1, len(@tmpstr)))
			set @pos = charindex(@delimiter, @tmpstr)
		end

		set @leftover = @tmpstr
	end

	if ltrim(rtrim(@leftover)) <> ''
		insert @tbl (string) values(@leftover)

	return
END	
GO

